plugins {
    id 'com.android.application'
}

def appReleaseTime = buildTime()
def appVersionName = '1.0.0000'
def appVersionCode = 1

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.luding.smartoa"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode appVersionCode
        versionName appVersionName
        flavorDimensions "versionCode"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("./luding.jks")
            storePassword "luding8808"
            keyAlias "luding"
            keyPassword "luding8808"
        }

        release {
            storeFile file("./luding.jks")
            storePassword "luding8808"
            keyAlias "luding"
            keyPassword "luding8808"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            buildConfigField "boolean", 'debugLog', 'false'

        }
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            buildConfigField "boolean", 'debugLog', 'true'
        }
    }

    productFlavors {
        dev {
            buildConfigField "boolean", "LOG_DEBUG", "true" //显示log
            buildConfigField("String", 'APP_URL', '"https://lmeeting-test-env.iflyrec.com/share/"')
        }
        internal_test {
            buildConfigField "boolean", "LOG_DEBUG", "true" //显示log
            buildConfigField("String", 'APP_URL', '"https://lmeeting-test-env.iflyrec.com/share/"')
        }
        product {
            buildConfigField "boolean", "LOG_DEBUG", "false" //不显示log
            buildConfigField("String", 'APP_URL', '"https://meeting.iflyrec.com/share/"')
        }

        applicationVariants.all { variant ->
            if (variant.buildType.name == 'release') {
                variant.outputs.all {
                    StringBuilder fileNameBuilder = new StringBuilder()
                    def name = System.getenv("name")
                    if (name) {
                        fileNameBuilder.append('name').append('_')
                    }
                    fileNameBuilder.append('LudingSmartOA').append('_')
                    if (variant.flavorName) {
                        fileNameBuilder.append(variant.flavorName).append('_')
                    }
                    if (variant.mergedFlavor.versionName) {
                        fileNameBuilder.append(variant.mergedFlavor.versionName).append('_')
                    }

                    if (variant.mergedFlavor.versionCode) {
                        fileNameBuilder.append(variant.mergedFlavor.versionCode).append('_')
                    }
                    if (variant.buildType.name) {
                        fileNameBuilder.append(variant.buildType.name).append('_')
                    }
                    fileNameBuilder.append(appReleaseTime).append('.apk')
                    outputFileName = fileNameBuilder.toString();
                }
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        dataBinding true
    }


}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    // restful api request
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.alibaba:fastjson:1.2.57'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.5'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    // RecyclerView Adapter BRVAH
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'
}

def buildTime() {
    return new Date().format('yyyyMMdd_HHmm')
}